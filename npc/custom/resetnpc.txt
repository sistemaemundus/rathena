//===== rAthena Script =======================================
//= Reset NPC
//===== Description: =========================================
//= Resets skills, stats, or both.
//===== Additional Comments: =================================
//= 1.0 First Version
//= 1.1 Optimized for the greater good. [Kisuka]
//= 1.2 Cleaning [Euphy]
//= 1.3 All statuses removed upon skill reset. [Euphy]
//= 1.4 Compressed Script, Added limit use option [Stolao]
//=	Changed set -> setarray, Improved text with F_InsertPlural
//= 1.5 Added sc_end_class to reset related status changes [sader1992]
//============================================================
//= Traduzido por Alek Emundus

prontera,150,193,4	script	Resetadora	124,{
	//	 		Skills,	Stats,	Both,	Limit
	setarray .@Reset,	5000,	5000,	9000,	0;
	mes "[Resetadora]";
	if(.@Reset[3] && reset_limit > .@Reset[3]) {
		mes "Voce so pode resetar "+callfunc("F_InsertPlural",.@Reset[3],"vezes")+" na sua vida.";
		close;
	}
	mes "Eu sou a Resetadora.";
	mes "Resetar Status: "+ callfunc("F_InsertComma",.@Reset[1]) +"z";
	mes "Resetar Skills: "+ callfunc("F_InsertComma",.@Reset[0]) +"z";
	mes "Resetar Ambos: "+ callfunc("F_InsertComma",.@Reset[2]) +"z";
	if(.@Reset[3]) mes "Voce so pode resetar "+callfunc("F_InsertPlural",.@Reset[3],"vezes")+", entao use "+((.@Reset[3]>1)?"isso":"com")+" cautela.";
	mes "Por favor selecione o servico que voce deseja:";
	next;
	set .@i,(select("^FF3355Resetar Skills:Resetar Status:Resetar Ambos^000000:Cancelar"));
	if(.@i > 3) close;
	mes "[Resetadora]";
	if (Zeny < .@Reset[.@i-1]) {
		mes "Desculpe, voce nao tem Zeny o suficiente.";
		close;
	}
	if(.@Reset[3]){
		mes "Voce so pode resetar "+callfunc("F_InsertPlural",.@Reset[3],"vezes")+" na sua vida, tem certeza?";
		if(select("Me deixe pensar:Parece otimo") == 1) close;
	}
	set Zeny, Zeny-.@Reset[.@i-1];
	if(.@i&1){
		sc_end_class;
		ResetSkill;
	}
	if(.@i&2) ResetStatus;
	mes "Aqui vamos nos!";
	if(.@Reset[3]) set reset_limit,reset_limit + 1;
	close;
}
