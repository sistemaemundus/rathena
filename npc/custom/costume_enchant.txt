//===== rAthena Script =======================================
//= Costume Enchanter
//===== By ===================================================
//= llchrisll
//===== Version ==============================================
//= 1.0 - Initial Version
//= 1.1 - Added support for all costume slots
//      - Added forgotten "Reset Enchantment" menu option
//= 1.2 - Added support to use an price to enchant
//      - Removed the unecessary variable for Reset Enchantment
//      - Modified the menu creation
//= 1.3 - Added requested features by Meneso on Discord:
//        Enchantment Chance
//        Selectable or Random Enchantment
//      - Added Cost Multiplier for higher Enchantment Levels
//      - Revamped Enchantment Config for better overview
//===== Tested With ==========================================
//= rAthena 03/29/2021 Revision
//= GIT Hash: 47c471e3d0e7997ae605ebf45760349f3b25d365
//===== Description ==========================================
//= This NPC allows players to enchant Costumes.
//= Selectable Enchants and reseting the Enchantments.
//===== Request =============================================
//= by Vegas (https://rathena.org/board/profile/53497-vegas/)
//= Topic: https://rathena.org/board/topic/115174-runen-enchanter-npc-for-custom-upper-middle-lower/
//============================================================
prontera,164,166,4	script	Encantador de Costume	101,{

disable_items;
mes .n$;
mes "Olá, "+strcharinfo(0)+"!";
mes "Se você quiser, posso encantar seu equipamento costume com qualquer encantamento que eu tenha em estoque!";
mes ( (.enchant_cost[0] > 0)?"Em troca de "+.enchant_cost[1]+"x "+getitemname(.enchant_cost[0])+ ( (.enchant_cost_m == 1)?", mas quanto maior o nível de encantamento, mais custa.":""):"de graça")+".";
mes "Também posso redefinir um encantamento "+ ((.enchant_reset[0] > 0)?", mas eu preciso de "+.enchant_reset[1]+"x "+getitemname(.enchant_reset[0]):"de graça")+".";
next;
switch(select("- Encantar Costumes!:- Resetar Encantamentos!:- Deixa para lá...")) {
	
	case 1:
	mes .n$;
	mes "Por favor selecione o costume que você quer que eu encante:";
	for ( set .@s,0; .@s < getarraysize(.equip_slot); set .@s,.@s + 1) {
		switch(.equip_slot[.@s]) {
			case EQI_COSTUME_HEAD_TOP: set .@c_slot$,"Top Headgear"; break;
			case EQI_COSTUME_HEAD_MID: set .@c_slot$,"Mid Headgear"; break;
			case EQI_COSTUME_HEAD_LOW: set .@c_slot$,"Low Headgear"; break;
			case EQI_COSTUME_GARMENT: set .@c_slot$,"Garment"; break;
			case EQI_SHADOW_ARMOR: set .@c_slot$,"Armor"; break;
			case EQI_SHADOW_WEAPON: set .@c_slot$,"Weapon"; break;
			case EQI_SHADOW_SHIELD: set .@c_slot$,"Shield"; break;
			case EQI_SHADOW_SHOES: set .@c_slot$,"Shoes"; break;
			case EQI_SHADOW_ACC_R: set .@c_slot$,"Accessory Right"; break;
			case EQI_SHADOW_ACC_L: set .@c_slot$,"Accessory Left"; break;
		}
		set .@c_m$,.@c_m$ + ((getequipid(.equip_slot[.@s]) != -1)?.@c_slot$ + " - "+getitemname(getequipid(.equip_slot[.@s])):"") + ( (.equip_slot[.@s+1] != 0)?":":"");
	}
	set .@c_m$,.@c_m$ + ":- Cancelar";
	set .@c,select(.@c_m$);
	if(.@c > getarraysize(.equip_slot)) close;
	set .@part,.equip_slot[.@c-1];
	next;
	mes .n$;
	if(getequipid(.@part) == -1) {
		mes "Parece que você não tem nenhum costume equipado aí.";
		close;
	}
	set .@hg,getequipid(.@part); // Saving Item ID
	set .@ref,getequiprefinerycnt(.@part); 	// Saving Refine Level, if there is one
	set .@card1,getequipcardid(.@part,0); // Save Item ID of Card Slot 1
	mes "Costume Selecionado: "+getitemname(getequipid(.@part));
	if(getequipcardid(.@part,3) != 0) {
		mes "Mas parece que você já tem um encantamento nesse costume.";
		close;
	}
	if(.enchant_type == 1) { // Selectable Enchantment
		mes "Agora selecione o Encantamento:";
		next;
		for ( set .@e,0; .@e < getarraysize(.enchant_id); set .@e,.@e + 3)
			set .@e_m$,.@e_m$ + "> "+getitemname(.enchant_id[.@e])+ ( (.enchant_id[.@e+3] != 0)?":":"");
		set .@rune,select(.@e_m$)*3-3;
		mes .n$;
		mes "Encantamento Selecionado: "+getitemname(.enchant_id[.@rune]);
		mes "Continuar?";
		if(select("- Sim!:- Não...") - 1) close;
		
	} else // Random Enchantment
		set .@rune,rand(getarraysize(.enchant_id)/3)*3-3;
		
	next;
	mes .n$;
	mes "Encantamento Selecionado: "+getitemname(.enchant_id[.@rune]);
	if(.enchant_cost[0] > 0) {
		mes "Preço para Encantar:";
		if(.enchant_cost_m == 1)
			mes (.enchant_cost[1]*.enchant_id[.@rune+2])+"x "+getitemname(.enchant_cost[0]);
		else
			mes .enchant_cost[1]+"x "+getitemname(.enchant_cost[0]);
	}
	if(.enchant_ch == 1)
		mes "Chance do Encantamento: "+.enchant_id[.@rune+1];
	mes "Continuar?";
	if(select("- Sim!:- Não...") - 1) close;
	next;
	mes .n$;
	if(.enchant_cost[0] > 0)
		if( (.enchant_cost_m == 1 && countitem(.enchant_cost[0]) < (.enchant_cost[1]*.enchant_id[.@rune+2]) ) || (.enchant_cost_m == 0 && countitem(.enchant_cost[0]) < .enchant_cost[1]) ) {
			mes "Parece que você não tem o suficiente "+getitemname(.enchant_cost[0])+"!";
			mes "Você precisa de pelo menos "+( (.enchant_cost_m == 1)?(.enchant_cost[1]*.enchant_id[.@rune+2]):.enchant_cost[1]);
			close;
		}
	if(.enchant_cost[0] > 0) {
		if(.enchant_cost_m == 1) 
			delitem .enchant_cost[0],(.enchant_cost[1]*.enchant_id[.@rune+2]);
		else if(.enchant_cost_m == 0)
			delitem .enchant_cost[0],.enchant_cost[1];
	}
	delequip .@part;
	if(.enchant_ch == 1) { // Enabled Enchantment Chance
		set .@r,100-.enchant_id[.@rune+1];
		if(rand(100) < .@r) {
			getitem2 .@hg,1,1,.@ref,0,.@card1,0,0,0;
			mes "Desculpe, o encantamento falhou.";
			close;
		}
	}
	getitem2 .@hg,1,1,.@ref,0,.@card1,0,0,.enchant_id[.@rune];
	mes "O encantamento foi um sucesso.";
	mes "Até a próxima.";
	enable_items;
	break;

	case 2:
	mes .n$;
	mes "Selecione o costume que você quer que eu reinicie o encantamento!";
	for ( set .@s,0; .@s < getarraysize(.equip_slot); set .@s,.@s + 1) {
		switch(.equip_slot[.@s]) {
			case EQI_COSTUME_HEAD_TOP: set .@c_slot$,"Top Headgear"; break;
			case EQI_COSTUME_HEAD_MID: set .@c_slot$,"Mid Headgear"; break;
			case EQI_COSTUME_HEAD_LOW: set .@c_slot$,"Low Headgear"; break;
			case EQI_COSTUME_GARMENT: set .@c_slot$,"Garment"; break;
			case EQI_SHADOW_ARMOR: set .@c_slot$,"Armor"; break;
			case EQI_SHADOW_WEAPON: set .@c_slot$,"Weapon"; break;
			case EQI_SHADOW_SHIELD: set .@c_slot$,"Shield"; break;
			case EQI_SHADOW_SHOES: set .@c_slot$,"Shoes"; break;
			case EQI_SHADOW_ACC_R: set .@c_slot$,"Accessory Right"; break;
			case EQI_SHADOW_ACC_L: set .@c_slot$,"Accessory Left"; break;
		}
		set .@c_m$,.@c_m$ + ((getequipid(.equip_slot[.@s]) != -1)?.@c_slot$ + " - "+getitemname(getequipid(.equip_slot[.@s])):"") + ( (.equip_slot[.@s+1] != 0)?":":"");
	}
	set .@c_m$,.@c_m$ + ":- Cancelar.";
	set .@c,select(.@c_m$);
	if(.@c > getarraysize(.equip_slot)) close;
	set .@part,.equip_slot[.@c-1];
	next;
	mes .n$;
	if(getequipcardid(.@part,3) == 0) {
		mes "Parece que você não tem nenhum encantamento nesse costume.";
		close;
	}
	set .@hg,getequipid(.@part); // Saving Item ID
	set .@ref,getequiprefinerycnt(.@part); 	// Saving Refine Level, if there is one
	set .@card1,getequipcardid(.@part,0); // Save Item ID of Card Slot 1
	mes "Costume Selecionado: "+getitemname(getequipid(.@part));
	if(.enchant_reset[0] > 0 && countitem(.enchant_reset[0]) < .enchant_reset[1]) {
		mes "Mas você não tem os itens necessários para reiniciar o encantamento!";
		close;
	}
	mes "Continuar?";
	if(select("- Sim!:- Não...") - 1) close;
	next;
	if(.enchant_reset[0]) 
		delitem .enchant_reset[0],.enchant_reset[1];
	delequip .@part;
	getitem2 .@hg,1,1,.@ref,0,.@card1,0,0,0;
	equip .@hg;
	mes .n$;
	mes "O encantamento foi reiniciado.";
	mes "Até a próxima.";
	break;
	
	case 3: break;
}
end;

OnInit:
set .n$,"[Encantador de Costume]";
// Enter here every Costume Slot, which you want to be enchantable
// Valid Entries:
// - EQI_COSTUME_HEAD_TOP
// - EQI_COSTUME_HEAD_MID
// - EQI_COSTUME_HEAD_LOW
// - EQI_COSTUME_GARMENT
// - EQI_SHADOW_ARMOR
// - EQI_SHADOW_WEAPON
// - EQI_SHADOW_SHIELD
// - EQI_SHADOW_SHOES
// - EQI_SHADOW_ACC_R
// - EQI_SHADOW_ACC_L
setarray .equip_slot[0],EQI_COSTUME_HEAD_TOP,EQI_COSTUME_HEAD_MID,EQI_COSTUME_HEAD_LOW;
// Enchantment Type
// 0 = Random
// 1 = Selectable
set .enchant_type,0;
// Enchantment Chance
// 1 = Enable
// 0 = Disable
set .enchant_ch,0;
// Price for enchanting:
// To disable the price, put 0 as values
setarray .enchant_cost[0],671,10; // Item ID,Amount
// Price Multiplier depending on Enchantment Level
// 1 = Enable
// 0 = Disable
set .enchant_cost_m,1;
// Enchantment Database:
// [0] = Item ID of Enchantment
// [1] = Enchantment Chance (if enabled)
// [2] = Price Multiplier (if enabled)
setarray .enchant_id[0],
// Array Index:			0	1	2
						4709,100,1,
						4719,90,2,
						4729,80,3,
						4739,70,4,
						4749,60,5,
						4759,50,6;
// Price for reseting:
// To disable the price, put 0 as values
setarray .enchant_reset[0],671,1; // Item ID,Amount
end;
}
